apply plugin: 'com.android.application'

android {

    /**
     * compileSdkVersion specifies the Android API level Gradle should use to
     * compile your app. This means your app can use the API features included in
     * this API level and lower.
     */
    compileSdkVersion 26

    /**
     * The defaultConfig block encapsulates default settings and entries for all
     * build variants, and can override some attributes in main/AndroidManifest.xml
     * dynamically from the build system. You can configure product flavors to override
     * these values for different versions of your app.
     */
    defaultConfig {
        /**
         * applicationId uniquely identifies the package for publishing.
         * However, your source code should still reference the package name
         * defined by the package attribute in the main/AndroidManifest.xml file.
         */
        applicationId "com.devindow.myfitnessroutines"

        // Defines the minimum API level required to run the app.
        minSdkVersion 15

        // Specifies the API level used to test the app.
        targetSdkVersion 31

        // Defines the version number of your app.
        versionCode 18

        // Defines a user-friendly version name for your app.
        versionName "1.0.8"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    /**
     * Added this to prevent source control from changing idea.misc back and forth between 1.8 & 1.7.
     */
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    /**
     * The buildTypes block is where you can configure multiple build types.
     * By default, the build system defines two build types: debug and release. The
     * debug build type is not explicitly shown in the default build configuration,
     * but it includes debugging tools and is signed with the debug key. The release
     * build type applies Proguard settings and is not signed by default.
     */
    buildTypes {
        /**
         * By default, Android Studio configures the release build type to enable code
         * shrinking, using minifyEnabled, and specifies the Proguard settings file.
         */
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    /**
     *  Specifies the flavor dimensions you want to use. The order in which you
     *  list each dimension determines its priority, from highest to lowest,
     *  when Gradle merges variant sources and configurations. You must assign
     *  each product flavor you configure to one of the flavor dimensions.
     */
    flavorDimensions "mode"
    productFlavors {
        free {
            dimension "mode"
            applicationIdSuffix ".free"
            versionNameSuffix "-free"
            resValue "string", "app_name", "My Fitness Routines - Free"
        }
        full {
            dimension "mode"
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
            resValue "string", "app_name", "My Fitness Routines"
        }
        yoga {
            dimension "mode"
            applicationIdSuffix ".yoga"
            versionNameSuffix "-yoga"
            resValue "string", "app_name", "My Yoga Routines"
        }
        abs {
            dimension "mode"
            applicationIdSuffix ".core"
            versionNameSuffix "-core"
            resValue "string", "app_name", "My Abs Routines"
        }
        soccer {
            dimension "mode"
            applicationIdSuffix ".soccer"
            versionNameSuffix "-soccer"
            resValue "string", "app_name", "My Soccer Routines"
        }
        taichi {
            dimension "mode"
            applicationIdSuffix ".taichi"
            versionNameSuffix "-taichi"
            resValue "string", "app_name", "My Tai Chi Routines"
        }
    }
    namespace 'com.devindow.myfitnessroutines'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:26.1.0'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'com.android.support:support-v4:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    /* Room for DB access */
    implementation 'android.arch.persistence.room:runtime:1.1.0-alpha3'
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.0-alpha3'
    testImplementation 'android.arch.persistence.room:testing:1.1.0-alpha3'
}
